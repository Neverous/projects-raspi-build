# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor:  Daenyth <Daenyth [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=rtorrent
pkgver=0.9.2
pkgrel=2
pkgdesc='Ncurses BitTorrent client based on libTorrent'
url='http://libtorrent.rakshasa.no/'
license=('GPL')
arch=('armv6zk')
depends=('libtorrent' 'curl' 'xmlrpc-c')
source=("http://libtorrent.rakshasa.no/downloads/${pkgname}-${pkgver}.tar.gz")
sha1sums=('2a642d722745091265037ed8929a23c237a3b99f')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
    ./configure \
        --prefix=/usr \
        --disable-debug \
        --with-xmlrpc-c

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -D doc/faq.xml "${pkgdir}"/usr/share/doc/rtorrent/faq.xml
    install -D doc/rtorrent.1 "${pkgdir}"/usr/share/man/man1/rtorrent.1
    install -D doc/rtorrent.rc "${pkgdir}"/usr/share/doc/rtorrent/rtorrent.rc
}
