# $Id: PKGBUILD 87246 2013-03-29 00:18:37Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=xmlrpc-c
pkgver=1.33.03
pkgrel=1
epoch=1
pkgdesc='XML-RPC for C and C++'
arch=(${RB_TARGET_ARCH})
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2')
makedepends=('libtool')
options=('!makeflags' '!libtool' '!emptydirs')
source=("$pkgname-advanced-$pkgver.tar.gz::http://xmlrpc-c.svn.sourceforge.net/viewvc/xmlrpc-c/advanced/?view=tar")

build() {
    cd "$srcdir/advanced"

    ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-libxml2-backend \
        --disable-cgi-server \
        --disable-libwww-client \
        --disable-wininet-client \
        --enable-abyss \
        --enable-cplusplus
    mkdir include/curl
    touch include/curl/types.h
    make CFLAGS_PERSONAL="$CFLAGS"
    make CFLAGS_PERSONAL="$CFLAGS" -C tools
}

package() {
    cd "$srcdir/advanced"

    make DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" -C tools install
    install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
sha256sums=('e23f24684739de8c0b497b1901138978aaa0f35557df1782adc4a85c6650547f')
