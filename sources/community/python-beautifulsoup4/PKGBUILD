# $Id: PKGBUILD 78212 2012-10-17 10:25:15Z allan $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# 06-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgbase=python-beautifulsoup4
pkgname=('python2-beautifulsoup4' 'python-beautifulsoup4')
pkgver=4.1.3
pkgrel=2
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping"
arch=('any')
url="http://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python-distribute' 'python2-distribute')
source=("http://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgbase#*-}-${pkgver}.tar.gz")
md5sums=('5aece3c0b8a080658155958111fa2fa9')

build() {
    cd "${srcdir}"
    cp -a beautifulsoup4-${pkgver}{,-python2}

    # Build python 3 module
    cd "${srcdir}/beautifulsoup4-${pkgver}"
    python setup.py build

    # Build python 2 module
    cd "${srcdir}/beautifulsoup4-${pkgver}-python2"
    python2 setup.py build
}

package_python2-beautifulsoup4() {
    depends=('python2-distribute')
    optdepends=('python2-chardet: universal encoding detector'
    'python2-lxml: pythonic binding for the libxml2 and libxslt libraries')

    cd "${srcdir}/beautifulsoup4-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

    rm -rf ${pkgdir}/usr/lib/python2.7/site-packages/bs4/tests
}

package_python-beautifulsoup4() {
    depends=('python-distribute')
    optdepends=('python-chardet: universal encoding detector'
    'python-lxml: pythonic binding for the libxml2 and libxslt libraries')

    cd "${srcdir}/beautifulsoup4-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

    rm -rf ${pkgdir}/usr/lib/python3.2/site-packages/bs4/{__pycache__,tests,builder/__pycache__}
}
