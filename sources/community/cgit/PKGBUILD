# $Id: PKGBUILD 88266 2013-04-15 11:13:22Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Current Maintainer:  Patrick Palka <patrick@parcs.ath.cx>
# Previous Maintainer:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Development: http://closure.ath.cx/aur-dev

# 06-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - disabled docs

pkgname=cgit
pkgver=0.9.1
_gitver=1.7.4
pkgrel=2
pkgdesc="A web interface for git written in plain C"
arch=(${RB_TARGET_ARCH})
url="http://hjemli.net/git/cgit"
license=('GPL')
depends=('openssl')
makedepends=('zlib' 'curl')
install=cgit.install
source=("http://git.zx2c4.com/cgit/snapshot/cgit-$pkgver.tar.xz"
        "git-${_gitver}.tar.gz::https://github.com/git/git/archive/v${_gitver}.tar.gz")
md5sums=('272ee8fec2d81ac1e65117578e64df1e'
         'c63cc7ca8a4bbf97c8802f354511223e')

build() {
    cd "$pkgname-$pkgver"
    _cgit_gitver=`grep 'GIT_VER = ' Makefile | cut -f3 -d\ `
    [ "x${_gitver}" == "x${_cgit_gitver}" ] || { msg "Bad git version"; return 1; }
    rm -rf git/*
    mv ../git-${_gitver}/* git/
    make
}

package() {
    cd "$pkgname-$pkgver"
    make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" install
    install -vDm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
    install -vd "$pkgdir/var/cache/$pkgname"
    mkdir -p $pkgdir/usr/lib/cgit
    mv $pkgdir/usr/share/webapps/cgit/cgit.cgi $pkgdir/usr/lib/cgit
    ln -sf ../../../lib/cgit/cgit.cgi $pkgdir/usr/share/webapps/cgit/cgit.cgi
}
