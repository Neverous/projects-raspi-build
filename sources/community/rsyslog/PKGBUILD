# $Id: PKGBUILD 88526 2013-04-19 10:32:19Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

# 07-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - removed rc.d script

pkgname=rsyslog
pkgver=7.2.7
pkgrel=1
pkgdesc="An enhanced multi-threaded syslogd with a focus on security and reliability"
url="http://www.rsyslog.com/"
arch=(${RB_TARGET_ARCH})
license=('GPL3')
depends=('zlib' 'libestr' 'libee' 'json-c')
makedepends=('postgresql-libs>=8.4.1' 'libmariadbclient' 'net-snmp' 'gnutls')
optdepends=('postgresql-libs: PostgreSQL Database Support'
            'libmariadbclient: MySQL Database Support'
            'net-snmp'
            'gnutls')
backup=('etc/rsyslog.conf'
        'etc/logrotate.d/rsyslog'
        'etc/conf.d/rsyslog')
options=('strip' 'zipman' '!libtool')
source=("http://www.rsyslog.com/files/download/rsyslog/rsyslog-$pkgver.tar.gz"
        'rsyslog.logrotate'
        'rsyslog.conf.d'
        'rsyslog.conf')
md5sums=('4a61d182acb5b5487e7b99dea8974857'
         '8065db4bef3061a4f000ba58779f6829'
         '18565f38a4445136446a31a3c95ffc3e'
         'd61dd424e660eb16401121eed20d98bc')

build() {
      cd ${srcdir}/${pkgname}-${pkgver}
      ./configure --prefix=/usr \
                  --disable-mysql \
                  --enable-pgsql \
                  --enable-mail \
                  --enable-imfile \
                  --enable-snmp \
                  --enable-gnutls \
                  --enable-inet \
                  --with-systemdsystemunitdir=/usr/lib/systemd/system
      echo "rsyslogd_LDADD += \$(LIBESTR_LIBS) -lm" >>tools/Makefile
      make
}
package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make install DESTDIR=${pkgdir}
    # Install Daemons and Configuration Files
    install -D -m644 doc/${pkgname}-example.conf ${pkgdir}/usr/share/doc/$pkgname/${pkgname}.conf.example
    install -D -m644 $srcdir/${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
    install -D -m644 $srcdir/${pkgname}.logrotate ${pkgdir}/etc/logrotate.d/${pkgname}
    install -D -m644 ${srcdir}/${pkgname}.conf.d ${pkgdir}/etc/conf.d/${pkgname}

    # fix location of systemctl and remove start precondition
    sed -i "$pkgdir/usr/lib/systemd/system/rsyslog.service" \
        -e 's@/bin/systemctl@/usr&@' \
        -e '/^ExecStartPre/d'
}
