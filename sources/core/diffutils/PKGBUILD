# $Id: PKGBUILD 180850 2013-03-27 01:10:49Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=diffutils
pkgver=3.3
pkgrel=1
pkgdesc="Utility programs used for creating patch files"
arch=(${RB_TARGET_ARCH})
url="http://www.gnu.org/software/diffutils"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
install=diffutils.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('99180208ec2a82ce71f55b0d7389f1b3'
         '203a2f2101dfcc09cdac655be353fd78')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
