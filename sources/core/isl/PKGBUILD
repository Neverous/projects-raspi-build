# $Id: PKGBUILD 182768 2013-04-15 01:13:42Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=isl
pkgver=0.11.2
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=(${RB_TARGET_ARCH})
url="http://www.kotnet.org/~skimo/isl/"
depends=('gmp')
license=('MIT')
options=('!libtool')
source=(http://www.kotnet.org/~skimo/isl/$pkgname-$pkgver.tar.bz2)
md5sums=('c40daa17d2995d1c98a0c1aca607541f')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make -j1 DESTDIR="$pkgdir/" install

    install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
    mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.10.1.2-gdb.py

    install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
