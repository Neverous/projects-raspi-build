# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - renamed to systemd_stage1 for rb_autbouild
#   - removed systemd-sysvcompat
#   - disabled introspection, gtk-doc

pkgbase=systemd
pkgname=('systemd_stage1')
pkgver=202
pkgrel=2
arch=('armv6zk')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
makedepends=('acl' 'cryptsetup' 'dbus-core' 'docbook-xsl' 'gperf'
             'intltool' 'kmod' 'libcap' 'libgcrypt'  'libmicrohttpd' 'libxslt'
             'linux-api-headers' 'pam' 'python' 'quota-tools' 'xz')
options=('!libtool')
source=("http://www.freedesktop.org/software/$pkgbase/$pkgbase-$pkgver.tar.xz"
        'initcpio-hook-udev'
        'initcpio-install-udev'
        'initcpio-install-timestamp'
        '0001-nss-myhostname-ensure-that-glibc-s-assert-is-used.patch'
        'use-split-usr-path.patch')
md5sums=('3136c6912d3ee1f6d4deb16234783731'
         'e99e9189aa2f6084ac28b8ddf605aeb8'
         'fb37e34ea006c79be1c54cbb0f803414'
         'df69615503ad293c9ddf9d8b7755282d'
         '3090c0b9b19b76731b4cf9c01c10c534'
         '76bf83fe34c5b40533abc5dc940576a6')

prepare() {
    cd "$pkgbase-$pkgver"

    # hang onto this until we do the /{,s}bin merge
    patch -Np1 <"$srcdir/use-split-usr-path.patch"

    patch -Np1 <"$srcdir/0001-nss-myhostname-ensure-that-glibc-s-assert-is-used.patch"
}

build() {
    cd "$pkgbase-$pkgver"

    ./configure \
        --enable-static \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sysconfdir=/etc \
        --disable-introspection \
        --disable-gtk-doc \
        --disable-audit \
        --disable-ima \
        --with-sysvinit-path= \
        --with-sysvrcnd-path= \
        --with-firmware-path="/usr/lib/firmware/updates:/usr/lib/firmware"

    make
}

check() {
    make -C "$pkgbase-$pkgver" check
}

package_systemd_stage1() {
    pkgdesc="system and service manager"
    depends=('acl' 'bash' 'dbus-core' 'glib2' 'kbd' 'kmod' 'hwids' 'libcap' 'libgcrypt'
        'pam' 'util-linux' 'xz')
    provides=("systemd=$pkgver" "libsystemd=$pkgver" 'nss-myhostname' "systemd-tools=$pkgver" "udev=$pkgver"
        'libgudev-1.0.so' 'libsystemd-daemon.so' 'libsystemd-id128.so'
        'libsystemd-journal.so' 'libsystemd-login.so' 'libudev.so')
    replaces=('systemd' 'libsystemd' 'nss-myhostname' 'systemd-tools' 'udev')
    conflicts=('systemd' 'libsystemd' 'nss-myhostname' 'systemd-tools' 'udev')
    optdepends=('cryptsetup: required for encrypted block devices'
        'libmicrohttpd: systemd-journal-gatewayd'
        'quota-tools: kernel-level quota management'
        'python: systemd library bindings'
        'systemd-sysvcompat: symlink package to provide sysvinit binaries')
    backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf)
    install="systemd.install"

    make -j1 -C "$pkgbase-$pkgver" DESTDIR="$pkgdir" install

    printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

    install -dm755 "$pkgdir/bin"
    ln -s ../usr/lib/systemd/systemd "$pkgdir/bin/systemd"

    # fix .so links in manpage stubs
    find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
        -exec sed -ri '1s|^\.so (.*)\.([0-9]+)|.so man\2/\1.\2|' {} +

    # don't write units to /etc by default -- we'll enable this on post_install
    # as a sane default
    rm "$pkgdir/etc/systemd/system/getty.target.wants/getty@tty1.service"
    rmdir "$pkgdir/etc/systemd/system/getty.target.wants"

    # get rid of RPM macros
    rm -r "$pkgdir/etc/rpm"

    # the path to udevadm is hardcoded in some places
    install -d "$pkgdir/sbin"
    ln -s ../usr/bin/udevadm "$pkgdir/sbin/udevadm"

    # udevd is no longer udevd because systemd. why isn't udevadm now udevctl?
    ln -s ../lib/systemd/systemd-udevd "$pkgdir/usr/bin/udevd"

    # add back tmpfiles.d/legacy.conf
    install -m644 "systemd-$pkgver/tmpfiles.d/legacy.conf" "$pkgdir/usr/lib/tmpfiles.d"

    # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
    sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
    s#GROUP="tape"#GROUP="storage"#g;
    s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules

    # add mkinitcpio hooks
    install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
    install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"
    install -Dm644 "$srcdir/initcpio-install-timestamp" "$pkgdir/usr/lib/initcpio/install/timestamp"

    ### split out manpages for sysvcompat
    rm -rf "$srcdir/_sysvcompat"
    install -dm755 "$srcdir"/_sysvcompat/usr/share/man/man8/
    mv "$pkgdir"/usr/share/man/man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8 \
        "$srcdir"/_sysvcompat/usr/share/man/man8
}
