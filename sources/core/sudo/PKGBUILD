# $Id: PKGBUILD 182700 2013-04-12 23:52:17Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=sudo
_sudover=1.8.6p8
pkgver=${_sudover/p/.p}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=(${RB_TARGET_ARCH})
url="http://www.sudo.ws/sudo/"
license=('custom')
groups=('base-devel')
depends=('glibc' 'pam')
backup=('etc/sudoers' 'etc/pam.d/sudo')
options=('!libtool')
source=(http://www.sudo.ws/sudo/dist/$pkgname-$_sudover.tar.gz{,.sig}
        sudo.pam)
sha256sums=('c0baaa87f59153967b650a0dde2f7d4147d358fa15f3fdabb47e84d0282fe625'
            'SKIP'
            'e7de79d2c73f2b32b20a8e797e54777a2bf19788ec03e48decd6c15cd93718ae')

build() {
    cd "$srcdir/$pkgname-$_sudover"

    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --with-logfac=auth \
        --with-pam \
        --with-env-editor \
        --with-passprompt="[sudo] password for %p: " \
        --with-all-insults
    make
}

check() {
    cd "$srcdir/$pkgname-$_sudover"
    make check
}

package() {
    cd "$srcdir/$pkgname-$_sudover"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/sudo.pam" "$pkgdir/etc/pam.d/sudo"

    install -Dm644 doc/LICENSE "$pkgdir/usr/share/licenses/sudo/LICENSE"
}
