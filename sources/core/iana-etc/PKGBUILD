# $Id: PKGBUILD 164065 2012-07-26 15:21:18Z bisson $
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=iana-etc
pkgver=2.30
pkgrel=3
pkgdesc='/etc/protocols and /etc/services provided by IANA'
url='http://sethwklein.net/iana-etc.html'
arch=('any')
license=('custom')
backup=('etc/'{protocols,services})
source=("http://sethwklein.net/${pkgname}-${pkgver}.tar.bz2"
        'newer.patch')
sha1sums=('218593bcb9264014c4e397d838b2c218eac9df06'
          '1e306cf77bc4e97cc020d6c53cb9de81c186a483')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -i ../newer.patch
    make get
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/iana-etc/LICENSE"
    install -Dm644 port-numbers.iana "${pkgdir}/usr/share/iana-etc/port-numbers.iana"
    install -Dm644 protocol-numbers.iana "${pkgdir}/usr/share/iana-etc/protocol-numbers.iana"
}
