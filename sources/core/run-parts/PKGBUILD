# $Id: PKGBUILD 167702 2012-10-03 08:21:43Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# 04-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=run-parts
pkgver=4.3.4
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('armv6zk')
url='http://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("ftp://ftp.archlinux.org/other/run-parts/debianutils_${pkgver}.tar.gz")
sha256sums=('1739976cb62b85e641ee36859a87dfb8e4c3214e1043b6bb00b56b87e96a19f5')

build() {
    cd $srcdir/debianutils-$pkgver
    ./configure --prefix=/usr
    make run-parts
}

package() {
    cd $srcdir/debianutils-$pkgver
    install -D -m755 run-parts $pkgdir/usr/bin/run-parts
    install -D -m644 run-parts.8 $pkgdir/usr/share/man/man8/run-parts.8
    for l in po4a/*/run-parts.8; do
        install -D -m644  ${l} $pkgdir/usr/share/man/${l:5:2}/man8/run-parts.8
    done
}
