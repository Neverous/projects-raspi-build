# $Id: PKGBUILD 183490 2013-04-22 09:10:17Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

# 04-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=gpgme
pkgver=1.4.0
pkgrel=1
pkgdesc="A C wrapper library for GnuPG"
arch=(${RB_TARGET_ARCH})
url="http://www.gnupg.org/related_software/gpgme/"
license=('GPL')
depends=('libgpg-error' 'pth' 'gnupg>=2')
install=${pkgname}.install
options=('!libtool' '!emptydirs')
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
md5sums=('a0f93aba6d8a831ba14905085027f2f9'
         'SKIP')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr --disable-static --disable-gpgsm-test
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
