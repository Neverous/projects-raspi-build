# $Id: PKGBUILD 148443 2012-01-31 23:47:54Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --shared to configure

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=zlib
pkgver=1.2.8
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('armv6zk')
license=('custom')
url="http://www.zlib.net/"
depends=('glibc')
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz")

build() {
    cd ${srcdir}/zlib-$pkgver
    ./configure --prefix=/usr --shared
    make

    grep -A 24 '^  Copyright' zlib.h > LICENSE
}

check() {
    cd ${srcdir}/zlib-$pkgver
    make test
}

package() {
    cd ${srcdir}/zlib-$pkgver
    make install DESTDIR=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
md5sums=('44d667c142d7cda120332623eab69f40')
