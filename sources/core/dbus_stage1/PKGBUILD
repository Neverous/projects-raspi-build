# $Id: PKGBUILD 172792 2012-12-06 01:48:03Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - renamed to dbus_stage1 for rb_autbouild
#   - disabled systemd
#   - removed rc.d script and xinit script

pkgname=dbus_stage1
pkgver=1.6.8
pkgrel=6
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(armv6zk)
license=('GPL' 'custom')
# do not depend on systemd to avoid circular dep,
# dep on shadow for install scriptlet FS#29341
depends=('expat' 'coreutils' 'filesystem' 'shadow')
makedepends=('libx11')
optdepends=('libx11: dbus-launch support')
provides=('dbus-core')
conflicts=('dbus-core')
replaces=('dbus-core')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz #{,.asc}
        systemd-user-session.patch)

build() {
    cd dbus-$pkgver
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
        --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
        --with-system-pid-file=/run/dbus/pid \
        --with-system-socket=/run/dbus/system_bus_socket \
        --with-console-auth-dir=/run/console/ \
        --enable-inotify --disable-dnotify \
        --disable-verbose-mode --disable-static \
        --disable-tests --disable-asserts \
        --with-systemdsystemunitdir=/usr/lib/systemd/system \
        --disable-systemd

    patch -p1 < "$srcdir/systemd-user-session.patch"

    make
}

package(){
    cd dbus-$pkgver
    make DESTDIR="$pkgdir" install

    rm -rf "$pkgdir/var/run"

    install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}
md5sums=('3bf059c7dd5eda5f539a1b7cfe7a14a2'
         'd8a1bd529b3ddca671ee1a695a143db9')
