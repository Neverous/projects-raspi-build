# $Id: PKGBUILD 162716 2012-06-28 20:04:34Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=pciutils
pkgver=3.1.10
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=(armv6zk)
license=('GPL2')
groups=('base')
url="http://mj.ucw.cz/sw/pciutils/"
depends=('glibc' 'hwids')
source=(#ftp://ftp.kernel.org/pub/software/utils/${pkgname}/${pkgname}-${pkgver}.tar.bz2
       ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/${pkgname}-${pkgver}.tar.gz)
md5sums=('cdb1e5a95ab7ac67349f10c8f77a6d3c')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
    cp lib/libpci.a "${srcdir}/"
    make clean
    make OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
    install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib/"
    # this is now supplied by the hwids package
    rm -rf $pkgdir/usr/{sbin/update-pciids,share/{man/man8/update-pciids.8,hwdata}}
}
