# $Id: PKGBUILD 165851 2012-09-01 14:29:46Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

# 04-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=libedit
pkgver=20120601_3.0
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='http://www.thrysoee.dk/editline/'
arch=('armv6zk')
license=('BSD')
depends=('ncurses')
options=('!libtool')
source=("http://www.thrysoee.dk/editline/libedit-${pkgver/_/-}.tar.gz")
sha1sums=('18ec27120f6abb222754e9f283d000fec0dcfc08')

build() {
    cd "${srcdir}/${pkgname}-${pkgver/_/-}"
    ./configure --prefix=/usr --enable-widec --enable-static=no
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver/_/-}"
    make prefix="${pkgdir}"/usr install

    cp "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/libedit/LICENSE
}
