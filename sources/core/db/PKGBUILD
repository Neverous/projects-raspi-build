# $Id: PKGBUILD 160636 2012-06-02 22:25:49Z allan $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=db
pkgver=5.3.21
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('armv6zk')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
depends=('gcc-libs' 'sh')
options=('!libtool')
install=db.install
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
sha1sums=('32e43c4898c8996750c958a90c174bd116fcba83')

build() {
    cd "${srcdir}"/$pkgname-${pkgver}/build_unix
    ../dist/configure --prefix=/usr --enable-compat185 \
        --enable-shared --enable-static --enable-cxx --enable-dbm
    make LIBSO_LIBS=-lpthread
}

package() {
    cd "${srcdir}"/$pkgname-${pkgver}/build_unix
    make DESTDIR="${pkgdir}" install
    rm -r "${pkgdir}"/usr/docs
    install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE \
        "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
