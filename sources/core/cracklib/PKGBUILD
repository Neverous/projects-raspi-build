# $Id: PKGBUILD 176334 2013-01-30 17:44:19Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>

# 03-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=cracklib
pkgver=2.8.22
pkgrel=1
pkgdesc="Password Checking Library"
arch=('armv6zk')
license=('GPL')
url="http://sourceforge.net/projects/cracklib"
depends=('glibc' 'zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('463177b5c29c7a598c991e12a4898e06')

build() {
    cd $srcdir/$pkgname-$pkgver
    ./configure --prefix=/usr --without-python
    make 
}

check() {
    cd $srcdir/$pkgname-$pkgver
    make check
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install
    install -Dm644 dicts/cracklib-small $pkgdir/usr/share/dict/cracklib-small
    sh ./util/cracklib-format dicts/cracklib-small \
        | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
