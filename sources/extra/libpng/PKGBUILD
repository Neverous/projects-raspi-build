# $Id: PKGBUILD 182372 2013-04-09 18:14:06Z ioni $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=libpng
pkgver=1.5.15
_apngver=1.5.15
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=(${RB_TARGET_ARCH})
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
md5sums=('e3122e7b5b5c36c034a4624c65f8a9d8'
         '3ae9ea7e4bd201f0b25e25cd6049b094')

build() {
    cd $pkgname-$pkgver

    # Add animated PNG (apng) support
    # see http://sourceforge.net/projects/libpng-apng/
    patch -p1 -i ../libpng-$_apngver-apng.patch

    ./configure --prefix=/usr
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    cd contrib/pngminus
    make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
    install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
    install -D -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
