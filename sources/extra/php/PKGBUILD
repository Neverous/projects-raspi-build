# $Id: PKGBUILD 182657 2013-04-12 07:45:16Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - disabled apache, embed, enchant, ldap, mssql

pkgbase=php
pkgname=('php'
         'php-cgi'
         'php-fpm'
         'php-pear'
         'php-gd'
         'php-intl'
         'php-mcrypt'
         'php-odbc'
         'php-pgsql'
         'php-pspell'
         'php-snmp'
         'php-sqlite'
         'php-tidy'
         'php-xsl')
pkgver=5.4.14
pkgrel=1
arch=('armv6zk')
license=('PHP')
url='http://www.php.net'
makedepends=('imap' 'postgresql-libs' 'postfix' 'libvpx'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'file'
             'libmcrypt' 'tidyhtml' 'aspell' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'libxslt' 'openssl' 'bzip2' 'db' 'gmp' 'freetype2')
source=("http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.bz2"
        'php.ini.patch' 'php-fpm.conf.in.patch'
        'logrotate.d.php-fpm' 'php-fpm.service' 'php-fpm.tmpfiles')
md5sums=('cfdc044be2c582991a1fe0967898fa38'
         '44c1bb1df346b3b9d3e092c7df9a37d0'
         '5c1f4c967b66ce3eaf819b46951dcbf5'
         'e036c721e5ad927cd1613c7475a58b3a'
         'f8b206cb8fecb05cb1ded8bee45633ff'
         'c60343df74f8e1afb13b084d5c0e47ed')

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}

    # adjust paths
    patch -p0 -i ${srcdir}/php.ini.patch
    patch -p0 -i ${srcdir}/php-fpm.conf.in.patch

    # workaround for pkg-config 0.28
    sed 's/ && test -n "$OPENSSL_INCS"//g' -i configure
}

build() {
    local _phpconfig="--srcdir=../${pkgbase}-${pkgver} \
        --config-cache \
        --prefix=/usr \
        --sysconfdir=/etc/php \
        --localstatedir=/var \
        --with-layout=GNU \
        --with-config-file-path=/etc/php \
        --with-config-file-scan-dir=/etc/php/conf.d \
        --disable-rpath \
        --mandir=/usr/share/man \
        --without-pear \
        "

    local _phpextensions="--enable-bcmath=shared \
        --enable-calendar=shared \
        --enable-dba=shared \
        --enable-exif=shared \
        --enable-ftp=shared \
        --enable-gd-native-ttf \
        --enable-intl=shared \
        --enable-mbstring \
        --enable-phar=shared \
        --enable-posix=shared \
        --enable-shmop=shared \
        --enable-soap=shared \
        --enable-sockets=shared \
        --enable-sysvmsg=shared \
        --enable-sysvsem=shared \
        --enable-sysvshm=shared \
        --enable-zip=shared \
        --with-bz2=shared \
        --with-curl=shared \
        --with-db4=/usr \
        --with-freetype-dir=/usr \
        --with-gd=shared \
        --with-gdbm \
        --with-gettext=shared \
        --with-gmp=shared \
        --with-iconv=shared \
        --with-icu-dir=/usr \
        --with-imap-ssl \
        --with-imap=shared \
        --with-jpeg-dir=/usr \
        --with-vpx-dir=/usr \
        --with-mcrypt=shared \
        --with-mhash \
        --with-mysql-sock=/var/run/mysqld/mysqld.sock \
        --with-mysql=shared,mysqlnd \
        --with-mysqli=shared,mysqlnd \
        --with-openssl=shared \
        --with-pcre-regex=/usr \
        --with-pdo-mysql=shared,mysqlnd \
        --with-pdo-odbc=shared,unixODBC,/usr \
        --with-pdo-pgsql=shared \
        --with-pdo-sqlite=shared,/usr \
        --with-pgsql=shared \
        --with-png-dir=/usr \
        --with-pspell=shared \
        --with-snmp=shared \
        --with-sqlite3=shared,/usr \
        --with-tidy=shared \
        --with-unixODBC=shared,/usr \
        --with-xmlrpc=shared \
        --with-xsl=shared \
        --with-zlib \
        "

    EXTENSION_DIR=/usr/lib/php/modules
    export EXTENSION_DIR
    PEAR_INSTALLDIR=/usr/share/pear
    export PEAR_INSTALLDIR

    cd ${srcdir}/${pkgbase}-${pkgver}

    # php
    mkdir ${srcdir}/build-php
    cd ${srcdir}/build-php
    ln -s ../${pkgbase}-${pkgver}/configure
    ./configure ${_phpconfig} \
        --disable-cgi \
        --with-readline \
        --enable-pcntl \
        ${_phpextensions}
    make

    # cgi and fcgi
    # reuse the previous run; this will save us a lot of time
    cp -a ${srcdir}/build-php ${srcdir}/build-cgi
    cd ${srcdir}/build-cgi
    ./configure ${_phpconfig} \
        --disable-cli \
        --enable-cgi \
        ${_phpextensions}
    make

    # fpm
    cp -a ${srcdir}/build-php ${srcdir}/build-fpm
    cd ${srcdir}/build-fpm
    ./configure ${_phpconfig} \
        --disable-cli \
        --enable-fpm \
        --with-fpm-user=http \
        --with-fpm-group=http \
        ${_phpextensions}
    make

    # pear
    cp -a ${srcdir}/build-php ${srcdir}/build-pear
    cd ${srcdir}/build-pear
    ./configure ${_phpconfig} \
        --disable-cgi \
        --with-readline \
        --enable-pcntl \
        --with-pear \
        ${_phpextensions}
    make
}

# check() {
# 	cd ${srcdir}/build-php
# 	make test
# }

package_php() {
    pkgdesc='An HTML-embedded scripting language'
    depends=('pcre' 'libxml2' 'bzip2' 'curl')
    replaces=('php-fileinfo' 'php-gmp' 'php-curl')
    provides=('php-fileinfo' 'php-gmp' 'php-curl')
    conflicts=('php-fileinfo' 'php-gmp' 'php-curl')
    backup=('etc/php/php.ini')

    cd ${srcdir}/build-php
    make -j1 INSTALL_ROOT=${pkgdir} install
    install -d -m755 ${pkgdir}/usr/share/pear
    # install php.ini
    install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/php.ini-production ${pkgdir}/etc/php/php.ini
    install -d -m755 ${pkgdir}/etc/php/conf.d/

    # remove static modules
    rm -f ${pkgdir}/usr/lib/php/modules/*.a
    # remove modules provided by sub packages
    rm -f ${pkgdir}/usr/lib/php/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
    # remove empty directory
    rmdir ${pkgdir}/usr/include/php/include
}

package_php-cgi() {
    pkgdesc='CGI and FCGI SAPI for PHP'
    depends=('php')

    install -D -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}/usr/bin/php-cgi
}

package_php-fpm() {
    pkgdesc='FastCGI Process Manager for PHP'
    depends=('php' 'systemd')
    backup=('etc/php/php-fpm.conf')
    install='php-fpm.install'

    install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/usr/sbin/php-fpm
    install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}/usr/share/man/man8/php-fpm.8
    install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}/etc/php/php-fpm.conf
    install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}/etc/logrotate.d/php-fpm
    install -d -m755 ${pkgdir}/etc/php/fpm.d
    install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php-fpm.conf
    install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php-fpm.service
}


package_php-pear() {
    pkgdesc='PHP Extension and Application Repository'
    depends=('php')
    backup=('etc/php/pear.conf')

    cd ${srcdir}/build-pear
    make install-pear INSTALL_ROOT=${pkgdir}
    rm -rf ${pkgdir}/usr/share/pear/.{channels,depdb,depdblock,filemap,lock,registry}
}

package_php-gd() {
    depends=('php' 'libpng' 'libjpeg' 'freetype2' 'libvpx')
    pkgdesc='gd module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}/usr/lib/php/modules/gd.so
}

package_php-intl() {
    depends=('php' 'icu')
    pkgdesc='intl module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}/usr/lib/php/modules/intl.so
}

package_php-mcrypt() {
    depends=('php' 'libmcrypt' 'libltdl')
    pkgdesc='mcrypt module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}/usr/lib/php/modules/mcrypt.so
}

package_php-odbc() {
    depends=('php' 'unixodbc')
    pkgdesc='ODBC modules for PHP'
    install -D -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}/usr/lib/php/modules/odbc.so
    install -D -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}/usr/lib/php/modules/pdo_odbc.so
}

package_php-pgsql() {
    depends=('php' 'postgresql-libs')
    pkgdesc='PostgreSQL modules for PHP'
    install -D -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}/usr/lib/php/modules/pgsql.so
    install -D -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}/usr/lib/php/modules/pdo_pgsql.so
}

package_php-pspell() {
    depends=('php' 'aspell')
    pkgdesc='pspell module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}/usr/lib/php/modules/pspell.so
}

package_php-snmp() {
    depends=('php' 'net-snmp')
    pkgdesc='snmp module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}/usr/lib/php/modules/snmp.so
}

package_php-sqlite() {
    depends=('php' 'sqlite')
    pkgdesc='sqlite module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}/usr/lib/php/modules/sqlite3.so
    install -D -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}/usr/lib/php/modules/pdo_sqlite.so
}

package_php-tidy() {
    depends=('php' 'tidyhtml')
    pkgdesc='tidy module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}/usr/lib/php/modules/tidy.so
}

package_php-xsl() {
    depends=('php' 'libxslt')
    pkgdesc='xsl module for PHP'
    install -D -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}/usr/lib/php/modules/xsl.so
}
