# $Id: PKGBUILD 173322 2012-12-18 22:53:36Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>

# 06-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - removed rc.d scripts

pkgname=ntp
pkgver=4.2.6.p5
_realver=4.2.6p5
pkgrel=13
pkgdesc='Network Time Protocol reference implementation'
url='http://www.ntp.org/'
license=('custom')
arch=('armv6zk')
depends=('openssl' 'libcap')
optdepends=('libedit: ntpdc, ntpq')
makedepends=('perl-html-parser' 'libedit')
backup=('etc/ntp.conf' 'etc/conf.d/ntpd.conf')
source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${_realver}.tar.gz"
        'ntpd'
        'ntpdate'
        'ntp.conf'
        'ntpd.conf'
        'ntpd.service'
        'ntpdate.service')
sha1sums=('4a5353a4791b6f4315a66c28d504ec6c7926b192'
          '4e324e625c1f080b5c028be5092aa71adbf9bd99'
          '01394b8a952f5edc85d19df8335eeac3980320f4'
          'f6a2292c26a201d9305669e99463fb54e5603b3f'
          '4537d1f58b299d463db5048129cb264511474b0b'
          'ffbabfaab90f1ba4dc769c84fbbfe89c5c01ee95'
          '0cb9dbd285e2e8ac51ff47dee08af69e12a09855'
)

install=install

build() {
    cd "${srcdir}/${pkgname}-${_realver}"

    ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-linuxcaps

    make
}

package() {
    cd "${srcdir}/${pkgname}-${_realver}"

    make DESTDIR="${pkgdir}" install

    rmdir "${pkgdir}"/usr/{lib,sbin}
    install -d -o 87 "${pkgdir}"/var/lib/ntp
    install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
    install -Dm644 ../ntpd.conf "${pkgdir}"/etc/conf.d/ntpd.conf
    install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
    install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
    install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    cd html
    ../scripts/html2man
    install -d "${pkgdir}"/usr/share/man
    mv man/man* "${pkgdir}"/usr/share/man
    mv "${pkgdir}/usr/share/man/man8/ntpd.8" "${pkgdir}/usr/share/man/man8/ntp-ntpd.8" # we should ditch openntpd

    install -dm755 "${pkgdir}/usr/lib/systemd/ntp-units.d"
    echo 'ntpd.service' > "${pkgdir}/usr/lib/systemd/ntp-units.d/${pkgname}.list"
}
