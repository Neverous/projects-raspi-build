# $Id: PKGBUILD 173177 2012-12-12 00:31:59Z eric $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>
#   - disabled zenmap

pkgname=nmap
pkgver=6.25
pkgrel=2
pkgdesc='Utility for network discovery and security auditing'
url='http://nmap.org/'
arch=('armv6zk')
license=('GPL')
depends=('pcre' 'openssl' 'libpcap' 'lua')
source=("http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('769943a1a5c10f67bf9738e26da42b3312db752f')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --mandir=/usr/share/man \
        --without-zenmap

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
