# $Id: PKGBUILD 180805 2013-03-26 11:49:34Z foutrelis $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Art Gramlich <art@gramlich-net.com>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=icu
pkgver=51.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=(armv6zk)
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs>=4.7.1-5' 'sh')
#makedepends=('clang')
source=(#http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver/./_}-src.tgz
	    http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
	    icu.8198.revert.icu5431.patch)
md5sums=('6eef33b229d0239d654983028c9c7053'
         'ebd5470fc969c75e52baf4af94a9ee82')

build() {
    cd ${srcdir}/icu/source

    # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
    patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man
    make
}

check() {
    cd "$srcdir/icu/source"
    make -k check # passes all
}

package() {
    cd ${srcdir}/icu/source
    make -j1 DESTDIR=${pkgdir} install

    # Install license
    install -Dm644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
