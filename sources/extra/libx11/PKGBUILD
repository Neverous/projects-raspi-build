# $Id: PKGBUILD 178618 2013-02-25 20:45:01Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

# 05-05-2013 Raspi-Build: Maciej Szeptuch (Neverous) <neverous@neverous.info>

pkgname=libx11
pkgver=1.5.0
pkgrel=2
pkgdesc="X11 client-side library"
arch=(armv6zk)
url="http://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
options=('!libtool')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2)
sha256sums=('c382efd7e92bfc3cef39a4b7f1ecf2744ba4414a705e3bc1e697f75502bd4d86')

build() {
    cd "${srcdir}/libX11-${pkgver}"
    ./configure --prefix=/usr --disable-static --disable-xf86bigfont
    make
}

check() {
    cd "${srcdir}/libX11-${pkgver}"
    make check
}

package() {
    cd "${srcdir}/libX11-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
