# $Id: PKGBUILD 165136 2012-08-11 13:41:56Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

# Raspi Build: Maciej Szeptuch <neverous@neverous.info>
# - disabled ldap

pkgname=krb5
pkgver=1.10.3
pkgrel=1
pkgdesc="The Kerberos network authentication system"
arch=('i686' 'x86_64' 'armv6zk')
url="http://web.mit.edu/kerberos/"
license=('custom')
depends=('e2fsprogs' 'keyutils')
makedepends=('perl')
backup=('etc/krb5.conf' 'var/lib/krb5kdc/kdc.conf')
source=(http://web.mit.edu/kerberos/dist/${pkgname}/1.10/${pkgname}-${pkgver}-signed.tar
        krb5-1.10.3-gcc47.patch
        krb5-kadmind
        krb5-kadmind.service
        krb5-kdc
        krb5-kdc.service
        krb5-kpropd
        krb5-kpropd.service
        krb5-kpropd@.service
        krb5-kpropd.socket)
options=('!emptydirs')
md5sums=('a31eaa949d663cccca6b790af4573368'
         '690dae196ce80d7ac9e64c91bafa9994'
         '9e1fd1857f1cbaffdce6379b63801a1b'
         'd2c898d376ebe5c62b873da7cab0f7e7'
         'b4a1fa6f4645c3da07d0fbcc0ade18f8'
         'f0245d33083337f95654f4caf1d32f57'
         'a5b43d06a15547d31f1dfdb1b3c5e7dc'
         '3dddf2f79ef74c4e736711e41228ee91'
         'babefac221331f7131a29faac33cc5bc'
         '5b9cee689e4f8085d39ef4e05f1ddd9a')

build() {
   tar zxvf ${pkgname}-${pkgver}.tar.gz
   cd "${srcdir}/${pkgname}-${pkgver}/src"

   # With gcc47 : deltat.c:1694:12: error: 'yylval' may be used uninitialized
   # in this function [-Werror=maybe-uninitialized]
   # As this is generated code, just ignore the complaint. + fixes for 1.10.3
   patch -Np2 -i ../../krb5-1.10.3-gcc47.patch
   rm lib/krb5/krb/deltat.c

   # FS#25384
   sed -i "/KRB5ROOT=/s/\/local//" util/ac_check_krb5.m4

   export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
   export CPPFLAGS+=" -I/usr/include/et"
   ./configure --prefix=/usr \
               --mandir=/usr/share/man \
               --localstatedir=/var/lib \
               --enable-shared \
               --with-system-et \
               --with-system-ss \
               --disable-rpath \
               --without-tcl \
               --enable-dns-for-realm \
               --without-ldap \
               --without-system-verto
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}/src"
   make DESTDIR="${pkgdir}" EXAMPLEDIR=/usr/share/doc/${pkgname}/examples install

   # Fix FS#29889
   #install -m 644 plugins/kdb/ldap/libkdb_ldap/kerberos.{ldif,schema} "${pkgdir}"/usr/share/doc/${pkgname}/examples

   # Sample KDC config file
   install -dm 755 "${pkgdir}"/var/lib/krb5kdc
   install -pm 644 config-files/kdc.conf "${pkgdir}"/var/lib/krb5kdc/kdc.conf

   # Default configuration file
   install -dm 755 "${pkgdir}"/etc
   install -pm 644 config-files/krb5.conf "${pkgdir}"/etc/krb5.conf

   install -dm 755 "${pkgdir}"/etc/rc.d
   install -m 755 ../../krb5-{kdc,kadmind,kpropd} "${pkgdir}"/etc/rc.d

   install -dm 755 "${pkgdir}"/usr/share/aclocal
   install -m 644 util/ac_check_krb5.m4 "${pkgdir}"/usr/share/aclocal

   install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/NOTICE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

   # systemd stuff
   install -dm 755 "${pkgdir}"/usr/lib/systemd/system
   install -m 644 ../../krb5-{kadmind.service,kdc.service,kpropd.service,kpropd@.service,kpropd.socket} \
      "${pkgdir}"/usr/lib/systemd/system
}
