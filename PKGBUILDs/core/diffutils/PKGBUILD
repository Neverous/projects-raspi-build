# $Id: PKGBUILD 137083 2011-09-06 04:35:04Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

# ALARM: add libsigsegv to depends

# Raspi Build: Maciej Szeptuch <neverous@neverous.info>
# - fix for missing gets
plugrel=1

pkgname=diffutils
pkgver=3.2
pkgrel=1.1
pkgdesc="Utility programs used for creating patch files"
arch=('i686' 'x86_64' 'armv6zk')
url="http://www.gnu.org/software/diffutils"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh' 'libsigsegv')
install=diffutils.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz
        gets.patch)
md5sums=('26ff64c332429c830c154be46b393382'
         '1ff3adfeca14e01cf303041030bdecf1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 < ${srcdir}/gets.patch
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
