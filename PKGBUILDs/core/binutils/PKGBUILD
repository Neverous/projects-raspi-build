# $Id: PKGBUILD 145910 2012-01-04 01:24:50Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
# - Added a CONFIGFLAG for ARM, defining our build hosts
# - Disabled make check, too processor/ram intensive, blows up

# Raspi-Build: Maciej Szeptuch <neverous@neverous.info>
# - patch for uninitialized variable in stabs.c

noautobuild=1

pkgname=binutils
pkgver=2.23
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64' 'armv6zk') 
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc>=2.16' 'zlib')
checkdepends=('dejagnu')
options=('!libtool' '!distcc' '!ccache')
install=binutils.install
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.gz{,.sig}
        stabs_uninitialized_fix.patch)
md5sums=('ed58f50d8920c3f1d9cb110d5c972c27'
         '5293d43d444852f71f7c96c6295ba66d'
         '496e15ed14fb6cf715ec900807e3b1db')

build() {
  cd ${srcdir}/binutils-$pkgver
  patch -Np1 < ${srcdir}/stabs_uninitialized_fix.patch

  cd ${srcdir}
  mkdir binutils-build && cd binutils-build

  [[ $CARCH == "x86_64" ]] && CONFIGFLAG="--enable-64-bit-bfd --disable-multilib"

  # ALARM: Specify build host types
  [[ $CARCH == "arm" ]] && CONFIGFLAG="--host=armv5tel-unknown-linux-gnueabi --build=armv5tel-unknown-linux-gnueabi"
  [[ $CARCH == "armv6h" ]] && CONFIGFLAG="--host=armv6l-unknown-linux-gnueabihf --build=armv6l-unknown-linux-gnueabihf"
  [[ $CARCH == "armv7h" ]] && CONFIGFLAG="--host=armv7l-unknown-linux-gnueabihf --build=armv7l-unknown-linux-gnueabihf"

  ${srcdir}/binutils-${pkgver}/configure --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --enable-ld=default --enable-gold \
    --enable-plugins --enable-threads \
    --enable-shared $CONFIGFLAG

  # check the host environment and makes sure all the necessary tools are available
  make configure-host

  make tooldir=${pkgdir}/usr

  # Rebuild libiberty.a with -fPIC
  cp -a libiberty libiberty-pic
  make -C libiberty-pic clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty-pic

  # Rebuild libbfd.a with -fPIC
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  cp -a bfd bfd-pic
  make -C bfd-pic clean
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd-pic

  # Rebuild libopcodes.a with -fPIC
  cp -a opcodes opcodes-pic
  make -C opcodes-pic clean
  make CFLAGS="$CFLAGS -fPIC" -C opcodes-pic
}

check() {
  cd ${srcdir}/binutils-build

  # do not abort on errors - manually check log files
  #make -k -j1 check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  
  # Add some useful headers
  install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/usr/include
  install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/usr/include

  # install libraries rebuilt with -fPIC
  install -m644 libiberty-pic/libiberty.a ${pkgdir}/usr/lib
  install -m644 bfd-pic/libbfd.a ${pkgdir}/usr/lib
  install -m644 opcodes/libopcodes.a ${pkgdir}/usr/lib

  # Remove Windows/Novell specific man pages
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so
}
