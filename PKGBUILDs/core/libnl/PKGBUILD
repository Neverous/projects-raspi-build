# $Id: PKGBUILD 162745 2012-06-29 11:46:12Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Raspi build: Maciej Szeptuch <neverous@neverous.info>
# - disabled LFLAGS, LDFLAGS because flex complains when they're enabled

pkgname=libnl
pkgver=3.2.11
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=(i686 x86_64 armv6zk)
url="http://www.infradead.org/~tgr/libnl/"
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
options=(!libtool)
source=("$url/files/$pkgname-$pkgver.tar.gz")
sha256sums=('9750409c0fec8b27966a65d94c040647ee01753e0947b0d59577b815ee0e710d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LDFLAGS=""
  export LFLAGS=""
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
